plugins {
	id 'c'
}

apply from: etUnitConverter

model {
	components {
		etrice_runtime_c_tests(NativeExecutableSpec) {
			sources.c {
				source {
					srcDirs = ['src']
					include '**/*.c'
				}
				exportedHeaders {
					srcDirs = ['src']
				}
				lib project: ':runtime:org.eclipse.etrice.runtime.c', library: 'etrice_runtime_c', linkage: 'static'
			}
		}
	}
}

def exeFile = "$buildDir/exe/etrice_runtime_c_tests/etrice_runtime_c_tests"
def etuFileRuntimeTest = 'log/testlog/TestCRuntime.etu'
def etuFileEtUnitTest = 'log/testlog/TestEtUnitSpecial.etu'

clean.delete 'log'

task run(type: Exec, dependsOn: assemble, group: 'verification') {
	commandLine exeFile
	inputs.file exeFile
	outputs.files etuFileRuntimeTest, etuFileEtUnitTest
	
	doFirst { file('log/testlog').mkdirs() }
}

createEtUnitConverterTask('convert', run)

check.dependsOn convert