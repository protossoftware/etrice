import static org.apache.tools.ant.taskdefs.condition.Os.*

plugins {
	id "c"
	id "de.protos.etunit-convert"
}

dependencies {
	etunitConverter project(":plugins:org.eclipse.etrice.etunit.converter")
}

model {
	components {
		etrice_runtime_c_tests(NativeExecutableSpec) {
			sources.c {
				source {
					srcDirs = ['src']
					include '**/*.c'
				}
				exportedHeaders {
					srcDirs = ['src']
				}
				lib project: ':runtime:org.eclipse.etrice.runtime.c', library: 'etrice_runtime_c', linkage: 'static'
			}
			binaries {
				all {
					cCompiler.args '-g3'
					if(isFamily(FAMILY_UNIX)) { linker.args '-lpthread', '-lrt', '-lm' }
				}
			}
		}
	}
}

def exeFile = "$buildDir/exe/etrice_runtime_c_tests/etrice_runtime_c_tests"
def etuFileRuntimeTest = "$buildDir/log/TestCRuntime.etu"
def etuFileEtUnitTest = "$buildDir/log/TestEtUnitSpecial.etu"

task run(type: Exec, dependsOn: assemble, group: 'verification') {
	executable exeFile
	workingDir = buildDir
	doFirst { file("$buildDir/log").mkdirs() }
}

etunitConvert {
	convert {
		source etuFileRuntimeTest, etuFileEtUnitTest
		dependsOn run
	}
}

check.dependsOn convert