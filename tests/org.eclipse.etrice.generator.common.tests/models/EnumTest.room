RoomModel EnumTest {

	import room.basic.types.* from "../../../runtime/${etModellib}/model/Types.room"
	import room.basic.test.* from "../../../runtime/${etModellib}/model/Tests.room"

	ActorClass EnumTest_Top {
		@TestInstance
		Structure {
			ActorRef user: EnumUser
		}
		Behavior { }
	}
	
	ActorClass EnumUser {
		Interface {
			Port prt: PC
		}
		Structure {
			external Port prt
			
			Attribute caseId: int32
			Attribute first: FirstEnum
			Attribute second: SecondEnum = "two"
			Attribute third: ThirdEnum
			Attribute fourth: FourthEnum
			Attribute fifth: FifthEnum
			
			Attribute arrayFirst[1] : FirstEnum
			Attribute arrayFourth[65] : FourthEnum = "sixtyfive"
			Attribute arrayFifth[2] : FifthEnum = "{f1, f2}"
			
		}
		Behavior {
			ctor {
				"caseId = etUnit_openAll(\"log\", \"EnumTest\", \"org.eclipse.etrice.generator.common.tests.EnumTest\", \"EnumTest_case\");"
			}
			dtor {
				"etUnit_closeAll(caseId);"
			}
			StateMachine {
				Transition init: initial -> state0 { }
				State state0 {
					entry {
						"EXPECT_EQUAL_INT32(caseId, \"first: initialized value\", 0, first);"
						"EXPECT_EQUAL_INT32(caseId, \"second: initialized value\", 2, second);"
						""
						"EXPECT_EQUAL_INT32(caseId, \"arrayFirst: initialized set value\", 0, arrayFirst[0]);"
						"EXPECT_EQUAL_INT32(caseId, \"arrayFouth: initialized set value\", 0x41, arrayFourth[20]);"
						"EXPECT_EQUAL_INT16(caseId, \"arrayFifth: initialized set value\", (short)0x01, arrayFifth[0]);"
						"EXPECT_EQUAL_INT16(caseId, \"arrayFifth: initialized set value\", (short)0x02, arrayFifth[1]);"
						""
						"first = FirstEnum.zero;"
						"second = SecondEnum.three;"
						"third = ThirdEnum.five;"
						"fourth = FourthEnum.sixtyfive;"
						"fifth = FifthEnum.f3;"
						""
						"EXPECT_EQUAL_INT32(caseId, \"first: newly set value\", 0, first);"
						"EXPECT_EQUAL_INT32(caseId, \"second: newly set value\", 3, second);"
						"EXPECT_EQUAL_INT32(caseId, \"third: newly set value\", 5, third);"
						"EXPECT_EQUAL_INT32(caseId, \"fourth: newly set value\", 0x41, fourth);"
						"EXPECT_EQUAL_INT16(caseId, \"fifth: newly set value\", (short)0x4, fifth);"
						""
						"etUnit_testFinished(caseId);"
					}
				}
			}
		}
	}
	
	ProtocolClass PC {
		incoming {
			Message m1(d: FirstEnum)
		}
	}
	
	Enumeration FirstEnum {
		zero, one, two, three
	}
	
	Enumeration SecondEnum {
		one=1, two, three
	}
	
	Enumeration ThirdEnum {
		one=1, two, five=5
	}
	
	Enumeration FourthEnum {
		one=1, three=3, sixtyfive=0x41
	}

	Enumeration FifthEnum of int16 {
		f1=0x1, f2=0x2, f3=0x4, f4=0x8
	}
}