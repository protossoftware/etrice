import static org.apache.tools.ant.taskdefs.condition.Os.*

apply plugin: "de.protos.etrice-base"
apply plugin: "de.protos.etunit-convert"

configurations {
	modelpath_c
	modelpath_cpp
	modelpath_java
}

dependencies {
	generator project(':plugins:org.eclipse.etrice.generator.c')
	generator project(':plugins:org.eclipse.etrice.generator.cpp')
	generator project(':plugins:org.eclipse.etrice.generator.java')

	modelpath_c project(path: ':runtime:org.eclipse.etrice.modellib.c', configuration: 'modelpathDir')
	modelpath_cpp project(path: ':runtime:org.eclipse.etrice.modellib.cpp', configuration: 'modelpathDir')
	modelpath_java project(path: ':runtime:org.eclipse.etrice.modellib.java', configuration: 'modelpathDir')

	etunitConverter project(":plugins:org.eclipse.etrice.etunit.converter")
}

def outputDir = "$buildDir/log"
def srcGenDir = "$buildDir/src-gen"

/**
 * Creates an eTrice generator task.
 * @param name the name of the new task
 * @param lang one of the following languages 'c', 'cpp', 'java'
 * @param modelDirs a list of model directories passed to the generator
 * @param genDir the generation directory
 * @param options the eTrice generator options
 * @return the generator task
 */
ext.createGenerateTask = { name, lang, modelDirs, genDir = 'src-gen', options = [] ->
	return modelSet.create(name) {
		module = "etrice-$lang"
		source.srcDir modelDirs
		source.include '**/*.room', '**/*.etmap', '**/*.etphys', '**/*.config'
		modelpath.from configurations."modelpath_$lang"
		genDir = project.layout.projectDirectory.file(genDir)
		option 'msc_instr'
		options.each { option it }
	}.generateTask
}

/**
 * Creates a tasks that converts etu files to xml files
 * @param name: The name of the new task
 * @param etuFiles: A list of etu files
 * @param args: The options passed to the converter
 */
ext.createEtUnitConverterTask = { name, etuFiles, args = [] ->
	return etunitConvert.create(name) {
		source etuFiles
		options.addAll(args)
	}
}

def getMainClass(String genDir) {
	// search for SubSystemRunner java file
	def mainFile = fileTree(genDir, { include '**/*Runner.java' }).getSingleFile()
	// relativize path to generation directory
	def relPath = file(genDir).toPath().relativize(mainFile.toPath()).normalize().toString()
	// remove '.java' at the end
	def path = relPath.substring(0, relPath.length() - 5)
	// replace all separators with '.'
	def mainClass = path.replace(File.separatorChar, '.' as char)
	return mainClass
}

/**
 * Creates all needed tasks to build an etrice application
 *
 * @param lang the eTrice generator type ('c', 'cpp', 'java')
 * @param name the name starting with a lower case letter
 * @param modelDirs the model directories
 * @param options options that are passed to the eTrice generator
 * @return the task to run the application
 */
ext.createBuild = { lang, name, modelDirs, options = [] ->
	
	def nameCap = name.capitalize()
	def genDir = "$srcGenDir/$name"
	def workingDir = "$outputDir/$name"
	
	def generateTask = createGenerateTask(name, lang, modelDirs, genDir, options)
		
	def runTask
	
	if(lang == 'c' || lang == 'cpp') {
		model {
			components {
				"$name"(NativeExecutableSpec) {
					sources."$lang" {
						builtBy generateTask
						source {
							srcDirs = [genDir]
							include "**/*.$lang"
						}
						exportedHeaders {
							srcDirs = [genDir]
						}
						lib project: ":runtime:org.eclipse.etrice.modellib.$lang", library: "etrice_modellib_$lang", linkage: 'static'
						if(lang == 'cpp') lib project: ':runtime:org.eclipse.etrice.runtime.cpp', library: 'etrice_runtime_cpp', linkage: 'static'
						lib project: ':runtime:org.eclipse.etrice.runtime.c', library: 'etrice_runtime_c', linkage: 'static'
					}
					binaries.all { 
						if(isFamily(FAMILY_UNIX)) {
							cCompiler.args '-g3', '-std=c99', '-lpthread'
							cppCompiler.args '-g3', '-std=c99'
							linker.args '-lpthread', '-lrt', '-lm'
						}
						else if(isFamily(FAMILY_WINDOWS)) {
							cCompiler.args '-g3', '-std=c99'
							cppCompiler.args '-g3', '-std=c99'
							linker.args '-lws2_32'
						}
					}
				}
			}
		}
		
		def exeFile = "$buildDir/exe/$name/$name"
		runTask = tasks.create(name: "run$nameCap", type: Exec) {
			dependsOn "${name}Executable"
			executable = exeFile
			args '-run_as_test'
			it.workingDir = workingDir
		}
	}
	
	else if(lang == 'java') {
		def testSourceSet = sourceSets.create(name) {
			java.srcDirs = [genDir]
		}
		
		dependencies {
			add "${name}Implementation", project(':runtime:org.eclipse.etrice.modellib.java')
			add "${name}Implementation", project(':runtime:org.eclipse.etrice.runtime.java')
		}
		
		tasks.getByName("compile${nameCap}Java").dependsOn generateTask
		build.dependsOn "${name}Classes"
		
		runTask = tasks.create(name: "run$nameCap", type: JavaExec) {
			dependsOn "${name}Classes"
			classpath = testSourceSet.runtimeClasspath
			args '-run_as_test'
			it.workingDir = workingDir
			
			doFirst { main = getMainClass(genDir) }
		}
	}
	
	runTask.doFirst { file("$workingDir/log").mkdirs() }
	return runTask
}

/**
 * Creates all needed tasks for one test execution
 *
 * @param lang the eTrice generator type ('c', 'cpp', 'java')
 * @param name the test name starting with a lower case letter
 * @param options options that are passed to the eTrice generator
 */
ext.createTest = { lang, name, options = [] ->

	def nameCap = name.capitalize()
	def langCap = lang.capitalize()
	def testName = "$name$langCap"
	def testNameCap = testName.capitalize()
	def suiteName = "${project.name}.$testNameCap"
	def genDir = "$srcGenDir/$testName"
	def workingDir = "$outputDir/$testName"
	def etuFile = "$workingDir/log/${nameCap}.etu"
		
	def runTask = createBuild(lang, testName, "model/$name", options)
	
	def convertTask = createEtUnitConverterTask("convert$testNameCap", [etuFile], ['-suite', suiteName])
	convertTask.dependsOn runTask
	
	check.dependsOn tasks.create(name: testName, dependsOn: convertTask, group: 'verification')
}
