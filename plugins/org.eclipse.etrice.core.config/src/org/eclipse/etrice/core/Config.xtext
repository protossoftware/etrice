/*******************************************************************************
 * Copyright (c) 2012 protos software gmbh (http://www.protos.de).
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * https://www.eclipse.org/legal/epl-2.0/
 *
 * SPDX-License-Identifier: EPL-2.0
 * 
 * CONTRIBUTORS:
 * 		Juergen Haug and Thomas Schuetz (initial contribution)
 * 		Eyrak Paen
 * 
 *******************************************************************************/
grammar org.eclipse.etrice.core.Config with org.eclipse.etrice.core.common.Base

generate config "http://www.eclipse.org/etrice/core/Config"
import "http://www.eclipse.org/etrice/core/common/Base"
import "http://www.eclipse.org/etrice/Room" as room

ConfigModel:
	'ConfigModel' name=FQN '{'
	imports+=Import*
	configElements+=ConfigElement*
	'}';

ConfigElement:
	SubSystemConfig | ActorClassConfig | ActorInstanceConfig | ProtocolClassConfig;

SubSystemConfig:
	'SubSystemConfig' root=[room::LogicalSystem|FQN] '/' subSystem=[room::SubSystemRef|ID] '{'
	dynConfig=DynamicConfig
	'}';

DynamicConfig:
	'dynamic configuration' '{'
	((('file path' filePath=STRING) |
	(('user import' userCode1=STRING)
	('user constructor' userCode2=STRING))) &
	('polling interval' polling=TIME)?)
	'}';

ActorClassConfig:
	'ActorClassConfig' actor=[room::ActorClass|FQN] '{'
	(attributes+=AttrClassConfig)*
	'}';

ActorInstanceConfig:
	'ActorInstanceConfig' root=[room::LogicalSystem|FQN] '/' subSystem=[room::SubSystemRef|ID] '/' path=RefPath '{'
	((attributes+=AttrInstanceConfig)* &
	(ports+=PortInstanceConfig)*)
	'}';

ProtocolClassConfig:
	'ProtocolClassConfig' protocol=[room::ProtocolClass|FQN] '{'
	(('regular' regular=PortClassConfig)? &
	(('conjugate' | 'conjugated') conjugated=PortClassConfig)?)
	'}';

PortClassConfig:
	{PortClassConfig} 'Port' '{'
	(attributes+=AttrClassConfig)*
	'}';

PortInstanceConfig:
	'InterfaceItem' item=[room::InterfaceItem|ID] '{'
	(attributes+=AttrInstanceConfig)*
	'}';

AttrConfig:
	AttrClassConfig | AttrInstanceConfig;

AttrClassConfig:
	'Attr' attribute=[room::Attribute] ('=' value=ConfigValueArray)? ('{'
	(('min' '=' min=NumberLiteral)?
	('max' '=' max=NumberLiteral)?
	(attributes+=AttrClassConfig)*)
	'}')?;

AttrInstanceConfig:
	'Attr' attribute=[room::Attribute] ('=' value=ConfigValueArray)? ('{'
	((dynConfig?='dynamic configuration' (readOnly?='read' | 'write'))?
	(attributes+=AttrInstanceConfig*))
	'}')?;

ConfigValueArray:
	values+=ConfigValue (',' values+=ConfigValue)*;

ConfigValue: LiteralConfigValue | EnumConfigValue;

LiteralConfigValue: value=Literal;

EnumConfigValue:
	type=[room::EnumerationType] '.' value=[room::EnumLiteral];

RefPath:
	refs+=RefSegment ('/' refs+=RefSegment)*;

RefSegment:
	ref=ID (':' idx=INT)?;
